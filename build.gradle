buildscript {
    repositories { mavenCentral() }
}

plugins {
    id 'com.github.spotbugs' version '1.7.1'
}

subprojects {

    repositories {
        mavenCentral()
    }

    apply plugin: "checkstyle"
    apply plugin: "pmd"
    apply plugin: "codenarc"

    pmd {
        toolVersion = "6.13.0"
        ruleSetFiles = rootProject.files("config/pmd/ruleset.xml")
        ruleSets = []
    }

    tasks.withType(Pmd) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    tasks.withType(Checkstyle) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    tasks.withType(CodeNarc) {
        configFile = rootProject.file("config/codenarc/ruleset.groovy")
    }

    spotbugs {
        effort = "max"
        reportLevel = "medium"
        includeFilter = rootProject.file("config/spotbugs/exclude.xml")
    }
}
